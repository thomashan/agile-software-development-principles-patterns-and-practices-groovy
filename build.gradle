plugins {
    id "org.sonarqube" version "2.7.1"
    id "com.github.kt3k.coveralls" version "2.8.4"
}

sonarqube {
    properties {
        property "sonar.projectName", "Exercises and Solutions from Agile Software Development Principles, Patterns, and Practices in Groovy"
        property "sonar.projectKey", "thomashan.github.io:agile-software-development-principles-patterns-and-practices-groovy"
    }
}

version project.VERSION

allprojects {
    apply plugin: "groovy"
    apply plugin: "eclipse"
    apply plugin: "idea"
    apply plugin: "jacoco"

    repositories {
        mavenLocal()
        mavenCentral()
    }

    sourceCompatibility = 1.9
    targetCompatibility = 1.9
    group "thomashan.github.io"

    dependencies {
        compile("org.codehaus.groovy:groovy-all:3.0.5") {
            exclude(group: "junit")
        }

        testCompile "org.junit.jupiter:junit-jupiter-api:5.6.2"
        testRuntime "org.junit.jupiter:junit-jupiter-engine:5.6.2"
    }

    compileGroovy {
        groovyOptions.optimizationOptions.indy = true
    }

    test {
        useJUnitPlatform()
    }

    jacocoTestReport {
        reports {
            xml.enabled = true // coveralls plugin depends on xml format report
            html.enabled = true
        }
    }

    jacoco {
        toolVersion = "0.8.2"
        reportsDir = file("$buildDir/customJacocoReportDir")
    }
}
